## Maintainer: khanhas <xuankhanh963@gmail.com>
pkgname=spicetify-cli
pkgver=1.1.0
pkgrel=1
pkgdesc='Command-line tool to customize Spotify client'
arch=('x86_64' 'i686')
url='https://github.com/khanhas/spicetify-cli'
license=('GPL')
makedepends=('git' 'go')
source=("${pkgname}-${pkgver}.tar.gz"::"$url/archive/v$pkgver.tar.gz")
sha256sums=('a47613da4227286fb5b1ab25b6e47fbc896c905d875e8b29c4763a11b2e94042')

build() {
  cd "$pkgname-$pkgver"
  export GOPATH="$srcdir"
  go build \
    -trimpath \
    -buildmode=pie \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    -o spicetify \
	.
}

check() {
  cd "$pkgname-$pkgver"
  ./spicetify -v
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 ./spicetify "$pkgdir"/usr/share/$pkgname/spicetify
  cp -r ./Themes ./Extensions ./CustomApps ./jsHelper ./globals.d.ts "$pkgdir"/usr/share/$pkgname

  rm -f ./shortcut
  echo "#!/usr/bin/env sh
/usr/share/$pkgname/spicetify \"\$@\"" >> ./shortcut

  install -Dm755 ./shortcut "$pkgdir"/usr/bin/spicetify

  # Clean up deps
  go clean -modcache
}
