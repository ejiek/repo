# Maintainer: w0rty <mawo97 at gmail.com>

pkgname=glab
pkgver=1.11.1
pkgrel=1
pkgdesc='Gitlab CLI tool helping to work seamlessly with Gitlab from the command line'
arch=('x86_64')
url="https://github.com/profclems/glab"
license=('MIT')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('0760db3187b7c488925464c87a88b10a4d60018796ac1a877e41d1eea4ac67ae')

prepare(){
  cd "${pkgname}-${pkgver}"
  mkdir -p build/
}

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  _builddate=$(date -u +%m/%d/%Y)
  go build -o build -trimpath -mod=readonly -buildmode=pie -ldflags=-linkmode=external -modcacherw ./cmd/glab/main.go
}

package() {
  cd "${pkgname}-$pkgver"
  install -Dm755 build/main "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
